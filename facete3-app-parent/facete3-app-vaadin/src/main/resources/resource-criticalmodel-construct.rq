# Query for computing a resource's critical metamodel.
# The is the set of ingoing / outgoing predicates with a value count
# that exceeds a threshold
#
# For each outgoing predicate this query also counts the number of list nodes
# (values with the rdf:list vocab)
#

SELECT * {

SERVICE <https://dbpedia.org/sparql> { {

SELECT ?src ?graph ?dir ?p ?vc ?lic {
    {
      { SELECT ?graph ?src ?p (COUNT(DISTINCT ?outTgt) AS ?vc) (COUNT(?ln) AS ?lic) {
        GRAPH ?graph {
          ?src ?p ?outTgt
          OPTIONAL {
            ?outTgt rdf:rest* ?ln .
            ?ln rdf:first ?f ; rdf:rest ?r .
          }
        }
      } GROUP BY ?graph ?src ?p }
      BIND(<urn:fwd> AS ?dir) # Because of Virtuoso this is last
    }
  UNION
    {
      { SELECT ?graph ?src ?p (COUNT(DISTINCT ?inTgt) AS ?vc) {
        GRAPH ?graph {
          ?inTgt ?p ?src
        }
      } GROUP BY ?graph ?src ?p }
      BIND(<urn:bwd> AS ?dir)
    }

    FILTER(?vc > 1000 || ?lic > 1000)

#    FILTER(?src = <http://dbpedia.org/resource/Leipzig>)
}


} }

}
